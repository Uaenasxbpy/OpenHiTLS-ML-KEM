module Parse where

type q    = 3329
type Byte = [8]
type W12  = [12]
type W16  = [16]

// 3 bytes -> 2x12-bit (与 C 同构)
BytesToUint12 : [3]Byte -> [2]W12
BytesToUint12 [b0, b1, b2] = [d1, d2]
  where
    [hi, lo] = (split b1) : [2][4]   // hi=b1[7..4], lo=b1[3..0]
    d1 = (zext b0 : W12) + ((zext lo : W12) << 8)
    d2 = (zext hi : W12) + ((zext b2 : W12) << 4)

IsHappyBlock : [3]Byte -> Bit
IsHappyBlock blk = (d1 < `q) && (d2 < `q)
  where
    [d1, d2] = BytesToUint12 blk

BlockToW16 : [3]Byte -> [2]W16
BlockToW16 blk = [ zext d1 : W16, zext d2 : W16 ]
  where
    [d1, d2] = BytesToUint12 blk

// -------------------- n=16, arrayLen=24 (k=8 blocks) --------------------
Blocks24 : [24]Byte -> [8][3]Byte
Blocks24 bs = (split bs) : [8][3]Byte

AllHappy24 : [24]Byte -> Bit
AllHappy24 bs = and (map IsHappyBlock (Blocks24 bs))

ParseHappyW16_16 : [24]Byte -> [16]W16
ParseHappyW16_16 bs = join (map BlockToW16 (Blocks24 bs))

// -------------------- n=32, arrayLen=48 (k=16 blocks) --------------------
Blocks48 : [48]Byte -> [16][3]Byte
Blocks48 bs = (split bs) : [16][3]Byte

AllHappy48 : [48]Byte -> Bit
AllHappy48 bs = and (map IsHappyBlock (Blocks48 bs))

ParseHappyW16_32 : [48]Byte -> [32]W16
ParseHappyW16_32 bs = join (map BlockToW16 (Blocks48 bs))
